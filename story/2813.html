<!doctype html><html lang="en"><head><meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no"><title>通过批处理脚本将文件上载到FTP站点</title><link rel="stylesheet" href="/css.css"><script>var _hmt = _hmt || [];(function() {var hm = document.createElement("script");hm.src = "https://hm.baidu.com/hm.js?f75329719d379998b2421a4cc2396366";var s = document.getElementsByTagName("script")[0]; s.parentNode.insertBefore(hm, s);})();</script></head><body><div id="header"><a class="site_title" href="http://www.fuzadu.com">复杂度</a></div><div><div><h2>通过批处理脚本将文件上载到FTP站点</h2></div><div class="content"><p>在电子邮件之外，向远程方发送文件的最常见方式可能是通过FTP。虽然有太多的FTP客户端可供选择，但Windows有一个鲜为人知且未得到充分利用的内置命令行FTP实用程序。这个工具的美妙之处在于它的脚本化能力，我们已经在下面的批处理脚本中利用了这一点。</p><p>可以从命令行使用此脚本作为“不问问题”的方法，使用单个命令上载一个或多个文件。此外，您还可以从批处理文件中调用此脚本来执行自动文件上载。此功能的几个用途包括(但当然不限于)：</p><p>
包括在备份脚本中以将数据发送到异地。
只需一个命令即可将html/php/等文件上传到Web服务器。
创建快捷方式以发送一组通用文件(如网站的源页)。</p><p>配置 / 构造 / 布局 / 外形</p><p>唯一需要的配置是设置FTP服务器连接信息。在“连接信息”行下，设置以下内容：</p><p>
服务器-您要上载到的FTP服务器。您可以输入DNS名称(ftp.myserver.com)或IP地址(1.2.3.4)。
用户名-您用于连接到FTP服务器的用户名。
密码-连接到FTP服务器的密码。</p><p>根据您的防火墙设置，在您第一次运行此脚本时，系统可能会提示您允许FTP连接到Internet。将此设置为从不再次提示您应该会删除将来的警告。</p><p>剧本</p><code>@ECHO OFF
ECHO Upload to FTP
ECHO Written by: Jason Faulkner
ECHO SysadminGeek.com
ECHO.
ECHO.

REM Usage:
REM UploadToFTP [/L] FileToUpload
REM
REM Required Parameters:
REM  FileToUpload
REM      The file or file containing the list of files to be uploaded.
REM
REM Optional Parameters:
REM  /L  When supplied, the FileToUpload is read as a list of files to be uploaded.
REM      A list of files should be a plain text file which has a single file on each line.
REM      Files listed in this file must specify the full path and be quoted where appropriate.

SETLOCAL EnableExtensions

REM Connection information:
SET Server=
SET UserName=
SET Password=

REM ---- Do not modify anything below this line ----

SET Commands="%TEMP%SendToFTP_commands.txt"

REM FTP user name and password. No spaces after either.
ECHO %UserName%> %Commands%
ECHO %Password%>> %Commands%

REM FTP transfer settings.
ECHO binary >> %Commands%

IF /I {%1}=={/L} (
   REM Add file(s) to the list to be FTP'ed.
   FOR /F "usebackq tokens=*" %%I IN ("%~dpnx2") DO ECHO put %%I >> %Commands%
) ELSE (
   ECHO put "%~dpnx1" >> %Commands%
)

REM Close the FTP connection.
ECHO close  >> %Commands%
ECHO bye    >> %Commands%

REM Perform the FTP.
FTP -d -i -s:%Commands% %Server%

ECHO.
ECHO.

REM Clean up.
IF EXIST %Commands% DEL %Commands%

ENDLOCAL</code><p>高尔夫球场 / 链接 / 沙地 / 沙丘</p><p>从sysadmin Geek下载上传到FTP脚本</p><div class="item_info"><span class="item_key"><a href="/tag/脚本/">脚本</a></span><span class="item_key"><a href="/tag/ftp/">ftp</a></span><span class="item_key"><a href="/tag/文件/">文件</a></span></div></div><div class="relate_story"><div><h3>相关文章</h3></div><ul><li><a href="/story/14323.html">如何在Windows上将Caps Lock用作修改键</a><li><a href="/story/14159.html">如何在Bash中使用变量</a><li><a href="/story/13819.html">如何将GUI添加到Linux Shell脚本</a><li><a href="/story/13415.html">如何在Linux上使用yes命令</a><li><a href="/story/13180.html">如何使用脚本编辑器为Google应用程序增压</a><li><a href="/story/13172.html">如何使用Linux睡眠命令暂停Bash脚本</a><li><a href="/story/13147.html">如何在进程退出时自动关闭MacOS终端</a><li><a href="/story/13134.html">如何编写自动热键脚本</a><li><a href="/story/12894.html">如何从Bash脚本获取系统的地理位置</a><li><a href="/story/11595.html">如何使用自动热键创建自定义键盘快捷键</a><li><a href="/story/11555.html">如何在Windows中自动键入带双空格的句点</a><li><a href="/story/11135.html">如何恢复Chromebook的原始BIOS和软件(如果已擦除)</a><li><a href="/story/10454.html">如何在Windows 10上创建和运行Bash Shell脚本</a><li><a href="/story/9334.html">如何使用Geektool自定义您的Mac桌面</a><li><a href="/story/7708.html">如何配置Windows以更轻松地使用PowerShell脚本</a><li><a href="/story/7704.html">如何使用批处理文件使PowerShell脚本更易于运行</a><li><a href="/story/7689.html">作用域如何影响PowerShell脚本</a><li><a href="/story/7581.html">使用Google Sheet和Google Apps脚本可以做的8件非常有用的事情</a><li><a href="/story/7410.html">愚蠢的极客小把戏：如何让你的电脑与你对话</a><li><a href="/story/7370.html">在Windows上使窗口始终在最上面的3种最佳方法</a></ul></div></div><div id="footer">&copy 2020 fuzadu.com</div></body></html>