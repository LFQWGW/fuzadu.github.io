<!doctype html><html lang="en"><head><meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no"><title>轻松将所有Zip归档文件转换为7z归档文件</title><link rel="stylesheet" href="/css.css"></head><body><div id="header"><a class="site_title" href="http://www.fuzadu.com">复杂度</a></div><div><div><h2>轻松将所有Zip归档文件转换为7z归档文件</h2></div><div class="content"><p>zip格式是文件压缩的标准，但是许多高级用户和系统管理员类型更喜欢使用7z格式，因为它提供了明显更好的压缩比。zip格式确实有一些优点，比如速度(相对于其他压缩格式)和应用程序支持。</p><p>因此，为了两全其美，我们创建了一个脚本，它可以用一个命令将您的zip文件转换成7z文件，我们称之为“深度存档”。</p><p>实际用途</p><p>那么，当您一开始就可以创建7z归档时，为什么还需要这个脚本呢？以下是几个原因：</p><p>
某些应用程序可能只生成zip格式的档案。
由于zip压缩比7z压缩更快，您可能希望快速生成一个zip文件，然后“深度压缩”它。</p><p>请考虑以下场景：</p><p>您运行一个FTP站点，客户将数据以zip格式上传给您。然后，您的用户根据需要下载并使用数据，但是您希望将这些文件保留一段时间，以防您的用户再次需要它们(在这种情况下，您是一个英雄，因为您随时可以使用它们)。为了节省一些空间，您可以将zip归档文件转换为7z格式并将其隐藏起来。</p><p>或者，将上述场景中客户上传的数据替换为备份的用户数据(或您能想到的任何其他数据)，您可以看到这方面有很多实际应用。</p><p>剧本</p><code>@ECHO OFF
ECHO Deep Archive
ECHO Written by: Jason Faulkner
ECHO SysadminGeek.com
ECHO.
ECHO.

REM Takes a Zip file and recompresses it as 7z archive.
REM Script process:
REM    1. Decompress the existing archive.
REM    2. Compress the extracted files in 7z format.
REM    3. (optional) Validate the new 7z file.
REM    4. (optional) Delete the source archive.
REM
REM Usage:
REM DeepArchive ZipFile
REM
REM Requirements:
REM    The 7-Zip command line tool (7za.exe) is in a location set in the PATH variable.
REM
REM Additional Notes:
REM This script processes a single zip archive.
REM To process all zip archives in a folder, use the ForFiles command from the command line:
REM    FORFILES /P "pathtozipfiles" /M *.zip /C "cmd /c DeepArchive @path"
REM
REM To run the archive compression/decompression as low priority background processes
REM add this in front of the 7ZA commands (DO NOT add this in front of the validation 7ZA command):
REM    START /BelowNormal /Wait 
REM Adding the above command will use a new window to perform these operations.

SETLOCAL EnableExtensions EnableDelayedExpansion

REM Should the deep archive file be validated? (1=yes, 0=no)
SET Validate=0

REM Compression level: 1,3,5,7,9 (higher=slower but more compression)
SET CompressLevel=5

REM Delete source zip file on success? (1=yes, 0=no)
SET DeleteSourceOnSuccess=1


REM ---- Do not modify anything below this line ----

SET ArchiveFile=%1
SET DeepFile=%ArchiveFile:.zip=.7z%
SET tmpPath=%TEMP%%~nx1
SET tmpPathZip="%tmpPath%*"
SET tmpPath="%tmpPath%"
SET tmpFile="%TEMP%tmpDeepArchive.txt"

IF NOT EXIST %tmpPath% (
   MKDIR %tmpPath%
) ELSE (
   RMDIR /S /Q %tmpPath%
)

ECHO Extracting archive: %ArchiveFile%
7ZA x %ArchiveFile% -o%tmpPath%
ECHO.

ECHO Compressing archive: %DeepFile%
7ZA a -t7z -mx%CompressLevel% %DeepFile% %tmpPathZip%
ECHO.

IF {%Validate%}=={1} (
   ECHO Validating archive: %DeepFile%
   7ZA t %DeepFile% | FIND /C "Everything is Ok" > %tmpFile%
   SET /P IsValid=< %tmpFile%
   IF !IsValid!==0 (
      ECHO Validation failed!
      DEL /F /Q %DeepFile%
      ECHO.
      GOTO Fail
   ) ELSE (
      ECHO Validation passed.
   )
   ECHO.
)
GOTO Success


:Success
IF {%DeleteSourceOnSuccess%}=={1} DEL /F /Q %ArchiveFile%
ECHO Success
GOTO End


:Fail
ECHO Failed
GOTO End


:End
IF EXIST %tmpFile% DEL /F /Q %tmpFile%
IF EXIST %tmpPath% RMDIR /S /Q %tmpPath%

ENDLOCAL
</code><p>高尔夫球场 / 链接 / 沙地 / 沙丘</p><p>从SysadminGeek.com下载DeepArchive脚本</p><p>下载7-Zip命令行工具</p><div class="item_info"><span class="item_key"><a href="/tag/zip/">zip</a></span><span class="item_key"><a href="/tag/数据/">数据</a></span><span class="item_key"><a href="/tag/文件/">文件</a></span></div></div><div class="relate_story"><div><h3>相关文章</h3></div><ul><li><a href="/story/13356.html">如何从Linux终端压缩或解压缩文件</a><li><a href="/story/12376.html">如何用iPhone或iPad的文件应用解压Zip文件</a><li><a href="/story/11060.html">最佳Windows文件归档程序</a><li><a href="/story/9869.html">如何用更好看的图标取代7-Zip的丑陋图标</a><li><a href="/story/8866.html">如何在OS X上打开7z和其他存档文件</a><li><a href="/story/8300.html">如何在Android设备上打开和创建Zip文件</a><li><a href="/story/6397.html">您需要了解的有关Zip文件的所有信息</a><li><a href="/story/4550.html">Windows下如何在不使用任何额外软件的情况下将Zip文件隐藏在图片中</a><li><a href="/story/3312.html">如何在Windows和Mac上免费解压.rar文件</a><li><a href="/story/2592.html">如何将非常大的文件上传到SkyDrive、Dropbox或电子邮件</a><li><a href="/story/997.html">愚蠢的极客花招：使用7-Zip作为快速文件浏览器</a><li><a href="/story/589.html">在Vista上防止资源管理器使用大型Zip文件冻结</a></ul></div></div><div id="footer">&copy 2020 fuzadu.com</div></body></html>